apply plugin: 'scala'
apply plugin: 'eclipse'

version = '0.0.1'

ext.launcherMain = 'org.kneelawk.hellojmonkeylauncher.HelloJMonkeyLauncher'

sourceSets {
	launcher {
		java {
			srcDir 'src/launcher/java'
			srcDir 'CPControl/src/main/java'
		}
	}
}

repositories {
	maven {
		name 'JMonkey'
		url 'http://dl.bintray.com/jmonkeyengine/org.jmonkeyengine'
	}
	jcenter()
}

dependencies {
	compile 'org.scala-lang:scala-library:2.12.5'
	compile 'org.jmonkeyengine:jme3-core:3.2.1-stable'
	compile 'org.jmonkeyengine:jme3-lwjgl3:3.2.1-stable'
	compile 'org.jmonkeyengine:jme3-desktop:3.2.1-stable'
	compile 'org.log4s:log4s_2.12:1.6.1'
}

// builds the launch jar
task launcherJar(type: Jar, dependsOn: [jar, launcherClasses]) {
	manifest { attributes 'Main-Class': launcherMain }
	baseName "${project.name}-all"
	from(configurations.compile) { into 'libs' }
	from('dependencies/natives') { into 'natives' }
	from(jar.outputs.files) { into 'app' }
	from(sourceSets.launcher.output)
	from('LICENSE')

	group 'Build'
	description 'Creates a jar with all dependencies included.'
}

assemble.dependsOn launcherJar
